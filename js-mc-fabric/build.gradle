plugins {
    id 'fabric-loom' version '1.10-SNAPSHOT'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version "8+"
}

group = rootProject.group

repositories {
    mavenCentral()
    maven { url "https://api.modrinth.com/maven" }
    maven { url "https://maven.nucleoid.xyz/" }
    maven { url = uri("https://repo.opencollab.dev/main/") }
    maven { url 'https://jitpack.io' }
}

configurations {
    embed
    compileOnly.extendsFrom(embed)
}

dependencies {
    embed implementation(project(':core'))
    // implementation project(':core')
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
    include(modImplementation('me.lucko:fabric-permissions-api:0.3.3'))

    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

    embed implementation("net.dv8tion:JDA:5.3.2") {
        exclude module: "opus-java"
    }

    embed implementation('club.minnced:discord-webhooks:0.8.4')
    embed implementation("de.erdbeerbaerlp:toml4j:dd60f51")
    compileOnly 'net.luckperms:api:5.4'
    compileOnly('org.geysermc.floodgate:api:2.2.4-SNAPSHOT')
    modCompileOnly "maven.modrinth:vanish:1.5.13+1.21.5"
    modImplementation("eu.pb4:placeholder-api:2.5.2+1.21.3")
}

shadowJar {
    configurations = [project.configurations.embed]
    // mergeServiceFiles()
    // exclude('META-INF/services/**')
    // Add relocate rules as needed
}

remapJar {
    dependsOn(shadowJar)
    mustRunAfter(shadowJar)
    inputFile = file(shadowJar.archiveFile)
}

tasks.build.dependsOn(tasks.shadowJar)

runServer {
    classpath += configurations.embed
}


publishing {
    publications {
        create("mavenJava", MavenPublication) {
            artifactId = 'fabric'
            from components.java
        }
    }
}

processResources {
    inputs.property "version", project.version
    inputs.property "minecraft_version", project.minecraft_version
    inputs.property "loader_version", project.loader_version
    filteringCharset "UTF-8"

    filesMatching("fabric.mod.json") {
        expand "version": project.version,
                "minecraft_version": project.minecraft_version,
                "loader_version": project.loader_version
    }
}

