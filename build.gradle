plugins {
    // Only apply plugins here if they are truly global (rare for multi-projects)
}

subprojects {
    apply plugin: 'java'

    repositories {
        mavenCentral()
        maven { url "https://api.modrinth.com/maven" }
        maven { url "https://maven.nucleoid.xyz/" }
        maven { url = uri("https://repo.opencollab.dev/main/") }
        maven { url 'https://jitpack.io' }
    }

    base {
        archivesName = "$rootProject.archives_base_name-$project.name"
    }

    tasks.withType(JavaCompile).configureEach {
        options.encoding = "UTF-8"
        options.release.set(21)
    }

    java {
        toolchain.languageVersion = JavaLanguageVersion.of(21)
        withSourcesJar()
    }

    afterEvaluate { subproject ->
        if (subproject.name in ['fabric', 'forge', 'paper']) {
            def includedModule = rootProject.findProperty('includeModule')
            if (includedModule && project(":${includedModule}").projectDir.exists()) {
                subproject.dependencies {
                    println "Module selected: ${includedModule}"
                    embed project(":${includedModule}")
                }
            } else {
                subproject.dependencies {
                    println "Default module selected: discord"
                    embed project(":discord")
                }
            }
        }
    }
}

